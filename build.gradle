configure(scalaProjects()) {
    apply plugin: 'scala'
    sourceCompatibility = 1.7

    dependencies {
        // Libraries needed to run the scala tools
        scalaTools 'org.scala-lang:scala-compiler:2.9.2'
        scalaTools 'org.scala-lang:scala-library:2.9.2'

        // Libraries needed for scala api
        compile 'org.scala-lang:scala-library:2.9.2'
        compile 'com.google.inject:guice:3.0'
    }
    task initProjectStructure() << {
        project.sourceSets*.allSource.srcDirs.flatten().each { dir ->
            dir.mkdirs()
        }
    }
}
configure(webProjects()) {
    apply plugin: 'war'
}
allprojects {
    apply plugin: 'idea'
    group = 'org.clarity.demo.cqrs'
    version = '1.0-SNAPSHOT'
    repositories {
        mavenLocal()
        mavenCentral()
        maven{
            url 'http://repo.typesafe.com/typesafe/releases'
        }
    }
}



def scalaProjects() {
    subprojects
}

def webProjects() {
    subprojects.findAll { project -> ['api', 'web'].contains(project.name) }
}


